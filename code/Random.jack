class Random
{
    field int seed;
    field int previousNumber;
    field int currentNumber;
    field int modulo;

    function int mod(int x, int y)
    {
        var int quotient, result;

        let quotient = x / y;
        let result = x - (quotient * y);
        if (result < 0)
        {
            let result = result + Math.abs(y);
        }

        return result;
    }

    constructor Random new(int inputSeed, int inputModulo)
    {
        let seed = inputSeed;
        let previousNumber = inputSeed;
        let currentNumber = Random.mod(inputSeed  * 11, inputModulo);
        let modulo = inputModulo;
        return this;
    }

    method void dispose()
    {
        do Memory.deAlloc(this);
        return;
    }

    method void setSeed(int inputSeed)
    {
        let seed = inputSeed;
        let previousNumber = inputSeed;
        let currentNumber = Random.mod(inputSeed  * 11, modulo);
        return;
    }

    method int getNext()
    {
        var int temporary;
        let temporary = currentNumber;
        let currentNumber = Random.mod(currentNumber + previousNumber, modulo);
        let previousNumber = temporary;
        return currentNumber;
    }
}